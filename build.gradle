plugins {
	id 'java'
	id 'org.springframework.boot' version '3.2.4'
	id 'io.spring.dependency-management' version '1.1.4'
//	used to integrate Protocol Buffers support into gradle project
	id 'com.google.protobuf' version '0.9.4'
}

protobuf {
	/*used to set the version of the Protobuf compiler*/
	protoc {
		artifact = "com.google.protobuf:protoc:3.25.1"
	}
	/*code generator(java code) from .proto files that works alongside the protocol buffers compiler*/
	plugins {
		grpc {
			artifact = 'io.grpc:protoc-gen-grpc-java:1.63.0'
		}
	}
	/*ensures that gRPC plugin is applied to all protobuf generation tasks*/
	generateProtoTasks {
		all()*.plugins {
			grpc {}
		}
	}
}

group = 'inc.nimbuspay'
version = '0.0.1-SNAPSHOT'

java {
	sourceCompatibility = '21'
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	/*necessary runtime support for gRPC communication over the Netty transport.*/
	runtimeOnly 'io.grpc:grpc-netty-shaded:1.63.0'
	/*support for serializing and deserializing protocol buffer messages used in gRPC communication.*/
	implementation 'io.grpc:grpc-protobuf:1.63.0'
	/*streaming support*/
	implementation 'io.grpc:grpc-stub:1.63.0'
	/*Generated annotation support*/
	implementation 'javax.annotation:javax.annotation-api:1.3.2'
	runtimeOnly 'org.postgresql:postgresql'
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa:3.2.4'
}

tasks.named('test') {
	useJUnitPlatform()
}
